plugins {
    id 'java'
    id 'io.github.goooler.shadow' version "8.1.7"
}

group = 'dev.unnm3d'
version = '4.4.3'

repositories {
    mavenCentral()
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        url 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.17.1-R0.1-SNAPSHOT'
    compileOnly 'com.github.MilkBowl:VaultAPI:1.7.1'
    compileOnly 'org.projectlombok:lombok:1.18.34'
    implementation 'net.kyori:adventure-text-minimessage:4.21.0'
    implementation 'net.kyori:adventure-platform-bukkit:4.4.0'
    compileOnly 'me.clip:placeholderapi:2.11.6'
    compileOnly 'io.lettuce:lettuce-core:6.5.5.RELEASE'
    implementation 'de.exlll:configlib-yaml:4.6.1'
    implementation 'com.github.Anon8281:UniversalScheduler:0.1.6'

    annotationProcessor 'org.projectlombok:lombok:1.18.34'
}

shadowJar {
    destinationDirectory.set(file("$rootDir/target"))
    archiveClassifier.set('')

    relocate 'net.kyori', 'dev.unnm3d.shaded.kyori'
    relocate 'de.exlll.configlib', 'dev.unnm3d.shaded.configlib'
    relocate 'org.snakeyaml.engine', 'dev.unnm3d.shaded.snakeyaml'
    relocate 'com.github.Anon8281.universalScheduler', 'dev.unnm3d.shaded.universalScheduler'
    minimize()
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
    filesMatching('source') {
        expand props
    }
}

